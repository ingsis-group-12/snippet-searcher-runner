import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.jetbrains.kotlin.jvm' version '1.9.23'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.9.23'
	id "org.jetbrains.kotlinx.kover" version "0.7.6"
	id 'org.jlleitschuh.gradle.ktlint' version '12.1.0'
}

group = 'ingsis.group-12'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	gradlePluginPortal()
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'org.jetbrains.kotlinx:kover-gradle-plugin:0.7.6'
	implementation 'org.jlleitschuh.gradle:ktlint-gradle:12.1.0'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.withType(KotlinCompile) {
	kotlinOptions {
		freeCompilerArgs += '-Xjsr305=strict'
		jvmTarget = '17'
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.check {
	dependsOn ktlintCheck
}

tasks.register('setupHooks') {
	doLast {
		def setupHooksFile = file("${rootProject.projectDir}/.githooks/setup-hooks")
		exec {
			commandLine setupHooksFile
		}
	}
}

tasks.register('checkGitHooksPath') {
	doLast {
		def desiredHooksPath = '.githooks'
		def currentHooksPath = 'git config core.hooksPath'.execute().text.trim()

		if (currentHooksPath != desiredHooksPath) {
			println "Git hooks path is not set correctly."
			println "Setting git hooks path to $desiredHooksPath"
			"git config core.hooksPath $desiredHooksPath".execute().waitFor()
			println "Git hooks path set to $desiredHooksPath"
		} else {
			println "Git hooks path is already set correctly."
		}
	}
}

build.dependsOn checkGitHooksPath
